<!DOCTYPE html>
<html>
	<head>
		<meta charset=utf-8>
		<title>FS 014</title>
		<style>
			body { margin: 0; }
			canvas { width: 100%; height: 100% }
		</style>
        
        <script id="fragmentShader" type="x-shader/x-fragment">
            #define M_PI 3.1415926535897932384626433832795
            
			uniform float time;
			uniform vec2 resolution;
			uniform sampler2D tPrev;
            uniform int rule;
            uniform int randomStart;

			varying vec2 vUv;
            
            //
            // 便利関数
            //
            vec2 normalizedPosition()
            {
                return -1.0 + 2.0 * vUv;
            }
            
            float rand(vec2 co){
                return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453);
            }
            
            //
            // scene
            //
            float initialize(vec2 p)
            {
                if (randomStart == 1)
                {
                    // ランダムスタート
                    if (int(abs(p.y + 0.9999) * resolution.y) == 0)
                    {
                        return floor(rand(gl_FragCoord.xy + vec2(time, time * time)) + 0.5);
                    }
                }
                else
                {
                    // 点からスタート
                    if (int(abs(p.x) * resolution.x) == 0)
                    {
                        return 1.0;
                    }
                }
                
                return 0.0;
            }
            
            vec2 loop(vec2 p)
            {
                p.x = mod(p.x, 1.0);
                return p;
            }
            
            float calc(vec2 p)
            {
                vec2 d = 1.0 / resolution;
                if (int(abs(p.y + 0.9999) * resolution.y) == 0)
                {
                    float p1 = texture2D(tPrev, loop(vUv - vec2(-d.x, d.y))).r;
                    float p2 = texture2D(tPrev, loop(vUv - vec2( 0.0, d.y))).r;
                    float p4 = texture2D(tPrev, loop(vUv - vec2( d.x, d.y))).r;
                    
                    float pattern = floor(p4 * 4.0 + p2 * 2.0 + p1 + 0.5);
                    
                    return mod(floor(float(rule) / pow(2.0, pattern)), 2.0);
                }
                return texture2D(tPrev, vUv - vec2(0.0, d.y)).r;
            }
            
            //
            // main
            //
			void main()
            {
                vec2 p = normalizedPosition();
                
                float l = 0.0;
                if (time < 0.1)
                {
                    l = initialize(p);
                }
                else
                {
                    l = calc(p);
                }
                
				gl_FragColor = vec4( vec3(l), 1.0 );
			}
        </script>

        <script id="vertexShader" type="x-shader/x-fragment">
            varying vec2 vUv;
            
            void main()
            {
                vUv = uv;
                vec4 mvPosition = modelViewMatrix * vec4( position, 1.0 );
                gl_Position = projectionMatrix * mvPosition;
            }
        </script>
	</head>
	<body>
		<p>
			<input id="buttonReset" type="button" value="Reset" onclick="onButtonResetClick()">
			<input id="buttonRandom" type="button" value="Random" onclick="onButtonRandomClick()">
			<input id="buttonStop" type="button" value="Stop" onclick="onButtonStopClick()">
            / Rule <input id="numberRule" type="number" value="30" min="0" max="255" step="1" oninput="onInputNumberRule(this.value)">
		</p>
        
        <script src="js/three.min.js"></script>
        <script src="js/scene.js"></script>
	</body>
</html>
